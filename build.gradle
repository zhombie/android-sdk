buildscript {
    ext.kotlin_version = '1.5.31'
   
    repositories {
        google()
        mavenCentral()
    }
   
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

/**
 * Shared configs across subprojects
 */
ext.projectArtifactId = { project ->
    if (project.name == 'activity' || project.name == 'context' ||
            project.name == 'drawable' || project.name == 'file' ||
            project.name == 'fragment' || project.name == 'json' ||
            project.name == 'kotlin' || project.name == 'recyclerview' ||
            project.name == 'text' || project.name == 'textview' ||
            project.name == 'view' || project.name == 'window') {
        return project.name
    } else {
        return null
    }
}

allprojects {
    group = 'com.github.zhombie.utils'
    version = '2.0.0'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)

    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

/**
 * Publishing and signing info
 */
subprojects { project ->
//    if (project.ext.artifactId == null) return

    if (name == 'sample') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'maven-publish'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 31
        buildToolsVersion "31.0.0"

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 31

            versionCode 1
            versionName "1.0"

            consumerProguardFiles "consumer-rules.pro"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        buildFeatures {
            buildConfig false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        kotlinOptions {
            jvmTarget = "11"
        }
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    url = "https://github.com/zhombie/q19.utils"

                    developers {
                        developer {
                            name = 'zhombie'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}